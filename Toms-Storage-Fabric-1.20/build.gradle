plugins {
	id 'fabric-loom' version '1.5-SNAPSHOT'
	id 'maven-publish'
}

repositories {
maven { url 'https://jitpack.io' }
jcenter()
maven { url "https://maven.shedaniel.me/" }
maven { url "https://maven.terraformersmc.com/releases/"}
maven { url "https://maven.architectury.dev/" }
maven {
		name = "Ladysnake Libs"
		url = 'https://maven.ladysnake.org/releases'
	}
maven {
    // location of the maven that hosts JEI files since January 2023
    name = "Jared's maven"
    url = "https://maven.blamejared.com/"
  }
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

dependencies {
	//to change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings loom.officialMojangMappings()
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
	
	modApi("me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    include "me.shedaniel.cloth:cloth-config-fabric:${project.cloth_config_version}"
	
	modImplementation("com.terraformersmc:modmenu:7.0.0-beta.2") {
        exclude(group: "net.fabricmc.fabric-api")
    }
    
    modImplementation "dev.emi:trinkets:3.7.0"
    modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.2.0"
	modImplementation "dev.onyxstudios.cardinal-components-api:cardinal-components-entity:5.2.0"
	
	modCompileOnly("me.shedaniel:RoughlyEnoughItems-api:${project.rei_version}"){
		exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "me.shedaniel.cloth")
        exclude(group: "dev.architectury")
    }
    modCompileOnly("me.shedaniel:RoughlyEnoughItems-default-plugin:${project.rei_version}") {
       exclude(group: "net.fabricmc.fabric-api")
       exclude(group: "me.shedaniel.cloth")
       exclude(group: "dev.architectury")
    }
    modRuntimeOnly("me.shedaniel:RoughlyEnoughItems-fabric:${project.rei_version}"){
        exclude(group: "net.fabricmc.fabric-api")
        exclude(group: "me.shedaniel.cloth")
        exclude(group: "dev.architectury")
    }
    modImplementation("dev.architectury:architectury-fabric:9.0.7")
    modCompileOnly "dev.emi:emi-fabric:1.0.5+1.20.1:api"
	//modLocalRuntime "dev.emi:emi-fabric:1.0.5+1.20.1"
    
	// compile against the JEI API but do not include it at runtime
	modCompileOnlyApi("mezz.jei:jei-1.20.1-common-api:15.2.0.22")
	modCompileOnlyApi("mezz.jei:jei-1.20.1-fabric-api:15.2.0.22")
	// at runtime, use the full JEI jar for Fabric
	modRuntimeOnly("mezz.jei:jei-1.20.1-fabric:15.2.0.22")
}

sourceSets {
	main {
		java {
			srcDir "../Toms-Storage-120/src/shared/java"
			srcDir "src/loader-shared/java"
			srcDir "../Toms-Storage-120/src/platform-shared/java"
		}
		resources {
			srcDir "../Toms-Storage-120/src/shared/resources"
			srcDir "src/loader-shared/resources"
			srcDir "../Toms-Storage-120/src/platform-shared/resources"
		}
	}
}

loom {
	accessWidenerPath = file("src/main/resources/tomsstorage.accesswidener")
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from "LICENSE"
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			// add all the jars that should be included when publishing to maven
			artifact(remapJar) {
				builtBy remapJar
			}
			artifact(sourcesJar) {
				builtBy remapSourcesJar
			}
		}
	}

	// select the repositories you want to publish to
	repositories {
		// uncomment to publish to the local maven
		// mavenLocal()
		def mavenURL = System.getProperty("mavenDir")
		if ("${mavenURL}" != "null") {
			maven {
				url = file(mavenURL)
			}
		}
	}
}
